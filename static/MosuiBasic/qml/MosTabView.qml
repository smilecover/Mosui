import QtQuick

import QtQuick.Layouts
import QtQuick.Templates as T
import MosuiBasic

T.Control {
    id: root
    clip: true

    enum TabPosition
    {
        Position_Top = 0,
        Position_Bottom = 1,
        Position_Left = 2,
        Position_Right = 3
    }

    enum TabType
    {
        Type_Default = 0,
        Type_Card = 1,
        Type_CardEditable = 2
    }

    enum TabSize
    {
        Size_Auto = 0,
        Size_Fixed = 1
    }

    enum TabAlign
    {
        Align_Center = 0,
        Align_Left = 1,
        Align_Right = 2
    }

    property bool animationEnabled: MosTheme.animationEnabled
    property var initModel: []
    property alias count: __tabModel.count
    property alias currentIndex: __tabView.currentIndex
    property int tabType: MosTabView.Type_Default
    property int tabSize: MosTabView.Size_Auto
    property int tabPosition: MosTabView.Position_Top
    property int tabAlign: MosTabView.Align_Center
    property bool tabAddable: false
    property bool tabCentered: false
    property bool tabCardMovable: true
    property int defaultTabWidth: 80
    property int defaultTabHeight: parseInt(themeSource.fontSize) + 16
    property alias defaultTabSpacing: root.spacing
    property int defaultTabIconSpacing: 2
    property int defaultTabLeftPadding: 8
    property int defaultTabRightPadding: 8
    property int defaultHighlightWidth: __private.isHorizontal ? 30 : 20
    property var addTabCallback:
        () => {
            append({ title: `New Tab ${__tabView.count + 1}` });
            positionViewAtEnd();
        }
    property var closeTabCallback:
        (index, data) => {
            remove(index);
        }
    property color colorTabCardBg: themeSource.colorTabCardBg
    property color colorTabCardBgActive: themeSource.colorTabCardBgActive
    property color colorTabCardBorder: themeSource.colorTabCardBorder
    property color colorTabCardBorderActive: themeSource.colorTabCardBorderActive
    property MosRadius radiusTabBg: MosRadius { all: themeSource.radiusTabBg }
    property var themeSource: MosTheme.MosTabView

    property Component addButtonDelegate: MosCaptionButton {
        id: __addButton
        animationEnabled: root.animationEnabled
        iconSize: parseInt(root.themeSource.fontSize)
        iconSource: MosIcon.PlusOutlined
        colorIcon: root.themeSource.colorTabCloseHover
        hoverCursorShape: Qt.PointingHandCursor
        radiusBg.all: root.themeSource.radiusButton
        onClicked: addTabCallback();
    }
    property Component tabDelegate: tabType == MosTabView.Type_Default ? __defaultTabDelegate : __cardTabDelegate
    property Component contentDelegate: Item { }
    property Component highlightDelegate: Item {
        Loader {
            id: __highlight
            width: __private.isHorizontal ? defaultHighlightWidth : 2
            height: __private.isHorizontal ? 2 : defaultHighlightWidth
            anchors {
                bottom: root.tabPosition == MosTabView.Position_Top ? parent.bottom : undefined
                right: root.tabPosition == MosTabView.Position_Left ? parent.right : undefined
            }
            state: __content.state
            states: [
                State {
                    name: 'top'
                    AnchorChanges {
                        target: __highlight
                        anchors.top: undefined
                        anchors.bottom: parent.bottom
                        anchors.left: undefined
                        anchors.right: undefined
                        anchors.horizontalCenter: parent.horizontalCenter
                        anchors.verticalCenter: undefined
                    }
                },
                State {
                    name: 'bottom'
                    AnchorChanges {
                        target: __highlight
                        anchors.top: parent.top
                        anchors.bottom: undefined
                        anchors.left: undefined
                        anchors.right: undefined
                        anchors.horizontalCenter: parent.horizontalCenter
                        anchors.verticalCenter: undefined
                    }
                },
                State {
                    name: 'left'
                    AnchorChanges {
                        target: __highlight
                        anchors.top: undefined
                        anchors.bottom: undefined
                        anchors.left: undefined
                        anchors.right: parent.right
                        anchors.horizontalCenter: undefined
                        anchors.verticalCenter: parent.verticalCenter
                    }
                },
                State {
                    name: 'right'
                    AnchorChanges {
                        target: __highlight
                        anchors.top: undefined
                        anchors.bottom: undefined
                        anchors.left: parent.left
                        anchors.right: undefined
                        anchors.horizontalCenter: undefined
                        anchors.verticalCenter: parent.verticalCenter
                    }
                }
            ]
            active: root.tabType === MosTabView.Type_Default
            sourceComponent: Rectangle {
                color: MosTheme.isDark ? root.themeSource.colorHightlightDark : root.themeSource.colorHightlight
            }
        }
    }

    function setCurrentIndex(index: int) {
        currentIndex = index;
    }

    function flick(xVelocity: real, yVelocity: real) {
        __tabView.flick(xVelocity, yVelocity);
    }

    function positionViewAtBeginning() {
        __tabView.positionViewAtBeginning();
    }

    function positionViewAtIndex(index: int, mode: int) {
        __tabView.positionViewAtIndex(index, mode);
    }

    function positionViewAtEnd() {
        __tabView.positionViewAtEnd();
    }

    function get(index: int): var {
        return __tabModel.get(index);
    }

    function set(index: int, object: var) {
        /*! 默认为true */
        if (object.editable === undefined)
            object.editable = true;
        __tabModel.set(index, object);
    }

    function setProperty(index: int, propertyName: string, value: var) {
        __tabModel.setProperty(index, propertyName, value);
    }

    function move(from: int, to: int, count = 1) {
        __tabModel.move(from, to, count);
    }

    function insert(index: int, object: var) {
        /*! 默认为true */
        if (object.editable === undefined)
            object.editable = true;
        __tabModel.insert(index, object);
    }

    function append(object: var) {
        /*! 默认为true */
        if (object.editable === undefined)
            object.editable = true;
        __tabModel.append(object);
    }

    function remove(index: int, count = 1) {
        __tabModel.remove(index, count);
    }

    function clear() {
        __tabModel.clear();
    }

    onInitModelChanged: {
        clear();
        /**
         * [Warning]
         * ListModel 的静态角色类型下, 如果某一条数据了单独的内容代理, 就必须同时为其他数据设置默认代理,
         * 所以我们这里需要进行两遍遍历, (另一种方式是设置 dynamicRoles, 但会大幅降低性能)
         */
        const hasContentDelegate = initModel.some(item => 'contentDelegate' in item);
        for (const object of initModel) {
            if (hasContentDelegate && !object.hasOwnProperty('contentDelegate')) {
                object.contentDelegate = root.contentDelegate;
            }
            append(object);
        }
    }

    objectName: '__MosTabView__'
    implicitWidth: Math.max(implicitBackgroundWidth + leftInset + rightInset,
                            implicitContentWidth + leftPadding + rightPadding)
    implicitHeight: Math.max(implicitBackgroundHeight + topInset + bottomInset,
                             implicitContentHeight + topPadding + bottomPadding)
    font {
        family: root.themeSource.fontFamily
        pixelSize: parseInt(root.themeSource.fontSize)
    }
    spacing: 2
    contentItem: Item {
        id: __contentItem

        MouseArea {
            anchors.fill: __tabView
            onWheel:
                wheel => {
                    if (__private.isHorizontal)
                    __tabView.flick(wheel.angleDelta.y * 6.5, 0);
                    else
                    __tabView.flick(0, wheel.angleDelta.y * 6.5);
                }
        }

        ListView {
            id: __tabView
            width: {
                if (__private.isHorizontal) {
                    const maxWidth = __contentItem.width - (__addButtonLoader.width + 5) * (root.tabCentered ? 2 : 1);
                    return (root.tabCentered ? Math.min(contentWidth, maxWidth) : maxWidth);
                } else {
                    return __private.tabMaxWidth;
                }
            }
            height: {
                if (__private.isHorizontal) {
                    return defaultTabHeight;
                } else {
                    const maxHeight = __contentItem.height - (__addButtonLoader.height + 5) * (root.tabCentered ? 2 : 1);
                    return (root.tabCentered ? Math.min(contentHeight, maxHeight) : maxHeight)
                }
            }
            clip: true
            onContentWidthChanged: if (__private.isHorizontal) cacheBuffer = contentWidth;
            onContentHeightChanged: if (!__private.isHorizontal) cacheBuffer = contentHeight;
            spacing: defaultTabSpacing
            move: Transition {
                NumberAnimation { properties: 'x,y'; duration: root.animationEnabled ? MosTheme.Primary.durationMid : 0 }
            }
            model: ListModel { id: __tabModel }
            delegate: tabDelegate
            highlight: highlightDelegate
            highlightMoveDuration: root.animationEnabled ? MosTheme.Primary.durationMid : 0
            orientation: __private.isHorizontal ? Qt.Horizontal : Qt.Vertical
            boundsBehavior: Flickable.StopAtBounds
            state: root.tabCentered ? (__content.state + 'Center') : __content.state
            states: [
                State {
                    name: 'top'
                    AnchorChanges {
                        target: __tabView
                        anchors.top: __contentItem.top
                        anchors.bottom: undefined
                        anchors.left: __contentItem.left
                        anchors.right: undefined
                        anchors.horizontalCenter: undefined
                        anchors.verticalCenter: undefined
                    }
                },
                State {
                    name: 'topCenter'
                    AnchorChanges {
                        target: __tabView
                        anchors.top: __contentItem.top
                        anchors.bottom: undefined
                        anchors.left: undefined
                        anchors.right: undefined
                        anchors.horizontalCenter: __contentItem.horizontalCenter
                        anchors.verticalCenter: undefined
                    }
                },
                State {
                    name: 'bottom'
                    AnchorChanges {
                        target: __tabView
                        anchors.top: undefined
                        anchors.bottom: __contentItem.bottom
                        anchors.left: __contentItem.left
                        anchors.right: undefined
                        anchors.horizontalCenter: undefined
                        anchors.verticalCenter: undefined
                    }
                },
                State {
                    name: 'bottomCenter'
                    AnchorChanges {
                        target: __tabView
                        anchors.top: undefined
                        anchors.bottom: __contentItem.bottom
                        anchors.left: undefined
                        anchors.right: undefined
                        anchors.horizontalCenter: __contentItem.horizontalCenter
                        anchors.verticalCenter: undefined
                    }
                },
                State {
                    name: 'left'
                    AnchorChanges {
                        target: __tabView
                        anchors.top: __contentItem.top
                        anchors.bottom: undefined
                        anchors.left: __contentItem.left
                        anchors.right: undefined
                        anchors.horizontalCenter: undefined
                        anchors.verticalCenter: undefined
                    }
                },
                State {
                    name: 'leftCenter'
                    AnchorChanges {
                        target: __tabView
                        anchors.top: undefined
                        anchors.bottom: undefined
                        anchors.left: __contentItem.left
                        anchors.right: undefined
                        anchors.horizontalCenter: undefined
                        anchors.verticalCenter: __contentItem.verticalCenter
                    }
                },
                State {
                    name: 'right'
                    AnchorChanges {
                        target: __tabView
                        anchors.top: __contentItem.top
                        anchors.bottom: undefined
                        anchors.left: undefined
                        anchors.right: __contentItem.right
                        anchors.horizontalCenter: undefined
                        anchors.verticalCenter: undefined
                    }
                },
                State {
                    name: 'rightCenter'
                    AnchorChanges {
                        target: __tabView
                        anchors.top: undefined
                        anchors.bottom: undefined
                        anchors.left: undefined
                        anchors.right: __contentItem.right
                        anchors.horizontalCenter: undefined
                        anchors.verticalCenter: __contentItem.verticalCenter
                    }
                }
            ]
        }

        Loader {
            id: __addButtonLoader
            active: root.tabAddable
            x: {
                switch (tabPosition) {
                case MosTabView.Position_Top:
                case MosTabView.Position_Bottom:
                    return Math.min(__tabView.x + __tabView.contentWidth + 5, __contentItem.width - width);
                case MosTabView.Position_Left:
                    return Math.max(0, __tabView.width - width);
                case MosTabView.Position_Right:
                    return Math.max(0, __tabView.x);
                }
            }
            y: {
                switch (tabPosition) {
                case MosTabView.Position_Top:
                    return Math.max(0, __tabView.y + __tabView.height - height);
                case MosTabView.Position_Bottom:
                    return __tabView.y;
                case MosTabView.Position_Left:
                case MosTabView.Position_Right:
                    return Math.min(__tabView.y + __tabView.contentHeight + 5, __contentItem.height - height);
                }
            }
            z: 10
            sourceComponent: addButtonDelegate
        }

        Item {
            id: __content
            state: {
                switch (tabPosition) {
                case MosTabView.Position_Top: return 'top';
                case MosTabView.Position_Bottom: return 'bottom';
                case MosTabView.Position_Left: return 'left';
                case MosTabView.Position_Right: return 'right';
                }
            }
            states: [
                State {
                    name: 'top'
                    AnchorChanges {
                        target: __content
                        anchors.top: __tabView.bottom
                        anchors.bottom: __contentItem.bottom
                        anchors.left: __contentItem.left
                        anchors.right: __contentItem.right
                    }
                },
                State {
                    name: 'bottom'
                    AnchorChanges {
                        target: __content
                        anchors.top: __contentItem.top
                        anchors.bottom: __tabView.top
                        anchors.left: __contentItem.left
                        anchors.right: __contentItem.right
                    }
                },
                State {
                    name: 'left'
                    AnchorChanges {
                        target: __content
                        anchors.top: __contentItem.top
                        anchors.bottom: __contentItem.bottom
                        anchors.left: __tabView.right
                        anchors.right: __contentItem.right
                    }
                },
                State {
                    name: 'right'
                    AnchorChanges {
                        target: __content
                        anchors.top: __contentItem.top
                        anchors.bottom: __contentItem.bottom
                        anchors.left: __contentItem.left
                        anchors.right: __tabView.left
                    }
                }
            ]

            Repeater {
                model: __tabModel
                delegate: Loader {
                    id: __contentLoader
                    anchors.fill: parent
                    sourceComponent: model.contentDelegate ?? root.contentDelegate
                    visible: __tabView.currentIndex === index
                    required property int index
                    required property var model
                }
            }
        }
    }

    Component {
        id: __defaultTabDelegate

        MosIconButton {
            id: __tabItem
            width: (!__private.isHorizontal && root.tabSize == MosTabView.Size_Auto) ? Math.max(__private.tabMaxWidth, tabWidth) : tabWidth
            height: tabHeight
            leftPadding: root.defaultTabLeftPadding
            rightPadding: root.defaultTabRightPadding
            iconSize: tabIconSize
            iconSource: tabIcon
            text: tabTitle
            animationEnabled: root.animationEnabled
            effectEnabled: false
            colorBg: 'transparent'
            colorBorder: 'transparent'
            colorText: {
                if (isCurrent) {
                    return MosTheme.isDark ? root.themeSource.colorHightlightDark : root.themeSource.colorHightlight;
                } else {
                    return down ? root.themeSource.colorTabActive :
                                  hovered ? root.themeSource.colorTabHover :
                                            root.themeSource.colorTab;
                }
            }
            font {
                family: root.themeSource.fontFamily
                pixelSize: parseInt(root.themeSource.fontSize)
            }
            contentItem: Item {
                implicitWidth: root.tabSize == MosTabView.Size_Auto ? (__text.width + calcIconWidth) : __tabItem.tabFixedWidth
                implicitHeight: Math.max(__icon.implicitHeight, __text.implicitHeight)

                property int calcIconWidth: __icon.status === Loader.Null ? 0 : (__icon.implicitWidth + __tabItem.tabIconSpacing)

                MosIconText {
                    id: __icon
                    width: __icon.status === Loader.Null ? 0 : implicitWidth
                    anchors.left: parent.left
                    anchors.verticalCenter: parent.verticalCenter
                    color: __tabItem.colorIcon
                    iconSize: __tabItem.iconSize
                    iconSource: __tabItem.iconSource
                    verticalAlignment: Text.AlignVCenter

                    Behavior on color { enabled: root.animationEnabled; ColorAnimation { duration: MosTheme.Primary.durationFast } }
                }

                MosText {
                    id: __text
                    width: root.tabSize === MosTabView.Size_Auto ? implicitWidth :
                                                                      Math.max(0, __tabItem.tabFixedWidth - parent.calcIconWidth)
                    anchors.left: __icon.right
                    anchors.leftMargin: __icon.status === Loader.Null ? 0 : __tabItem.tabIconSpacing
                    anchors.verticalCenter: parent.verticalCenter
                    text: __tabItem.text
                    font: __tabItem.font
                    color: __tabItem.colorText
                    elide: Text.ElideRight
                    horizontalAlignment: {
                        switch (root.tabAlign) {
                        case MosTabView.Align_Left: return Text.AlignLeft;
                        case MosTabView.Align_Right: return Text.AlignRight;
                        default: return Text.AlignHCenter;
                        }
                    }

                    Behavior on color { enabled: root.animationEnabled; ColorAnimation { duration: MosTheme.Primary.durationFast } }
                }
            }
            onTabWidthChanged: {
                if (index == 0)
                    __private.tabMaxWidth = 0;
                __private.tabMaxWidth = Math.max(__private.tabMaxWidth, __tabItem.tabWidth);
            }
            onClicked: __tabView.currentIndex = index;

            required property int index
            required property var model
            property bool isCurrent: __tabView.currentIndex === index
            property string tabKey: model.key || ''
            property var tabIcon: model.iconSource || 0
            property int tabIconSize: model.iconSize || parseInt(root.themeSource.fontSize)
            property int tabIconSpacing: model.iconSpacing || root.defaultTabIconSpacing
            property string tabTitle: model.title || ''
            property int tabFixedWidth: model.tabWidth || root.defaultTabWidth
            property int tabWidth: root.tabSize == MosTabView.Size_Auto ? (implicitContentWidth + leftPadding + rightPadding) :
                                                                             implicitContentWidth
            property int tabHeight: model.tabHeight || root.defaultTabHeight
        }
    }

    Component {
        id: __cardTabDelegate

        Item {
            id: __tabContainer
            width: __tabItem.width
            height: __tabItem.height

            required property int index
            required property var model
            property alias tabItem: __tabItem
            property bool isCurrent: __tabView.currentIndex === index
            property string tabKey: model.key || ''
            property var tabIcon: model.iconSource || 0
            property int tabIconSize: model.iconSize || parseInt(root.themeSource.fontSize)
            property int tabIconSpacing: model.iconSpacing || root.defaultTabIconSpacing
            property string tabTitle: model.title || ''
            property int tabFixedWidth: model.tabWidth || root.defaultTabWidth
            property int tabWidth: __tabItem.calcWidth
            property int tabHeight: model.tabHeight || root.defaultTabHeight

            property bool tabEditable: model.editable && root.tabType == MosTabView.Type_CardEditable

            onTabWidthChanged: {
                if (__private.needResetTabMaxWidth) {
                    __private.needResetTabMaxWidth = false;
                    __private.tabMaxWidth = 0;
                }
                __private.tabMaxWidth = Math.max(__private.tabMaxWidth, __tabItem.calcWidth);
            }

            MosRectangleInternal {
                id: __tabItem
                z: __dragHandler.drag.active ? 1 : 0
                width: (!__private.isHorizontal && root.tabSize == MosTabView.Size_Auto) ? __private.tabMaxWidth : calcWidth
                height: __tabContainer.tabHeight
                color: isCurrent ? root.colorTabCardBgActive : root.colorTabCardBg
                border.color: isCurrent ? root.colorTabCardBorderActive : root.colorTabCardBorder
                topLeftRadius: root.tabPosition == MosTabView.Position_Top ||
                               root.tabPosition == MosTabView.Position_Left ? root.radiusTabBg.topLeft : 0
                topRightRadius: root.tabPosition == MosTabView.Position_Top ||
                                root.tabPosition == MosTabView.Position_Right ? root.radiusTabBg.topRight : 0
                bottomLeftRadius: root.tabPosition == MosTabView.Position_Bottom ||
                                  root.tabPosition == MosTabView.Position_Left ? root.radiusTabBg.bottomLeft : 0
                bottomRightRadius: root.tabPosition == MosTabView.Position_Bottom ||
                                   root.tabPosition == MosTabView.Position_Right ? root.radiusTabBg.bottomRight : 0

                property bool down: false
                property bool hovered: false
                property int calcIconWidth: __icon.status === Loader.Null ? 0 : (__icon.implicitWidth + __tabContainer.tabIconSpacing)
                property int calcCloseWidth: __close.visible ? (__close.implicitWidth + __tabContainer.tabIconSpacing + root.defaultTabRightPadding) : 0
                property real calcWidth: root.tabSize == MosTabView.Size_Auto ? (__text.width + calcIconWidth + calcCloseWidth)
                                                                                 : __tabContainer.tabFixedWidth
                property real calcHeight: Math.max(__icon.implicitHeight, __text.implicitHeight, __close.height)
                property color colorText: {
                    if (isCurrent) {
                        return MosTheme.isDark ? root.themeSource.colorHightlightDark : root.themeSource.colorHightlight;
                    } else {
                        return down ? root.themeSource.colorTabActive :
                                      hovered ? root.themeSource.colorTabHover :
                                                root.themeSource.colorTab;
                    }
                }

                Behavior on color { enabled: root.animationEnabled; ColorAnimation { duration: MosTheme.Primary.durationMid } }

                MouseArea {
                    id: __dragHandler
                    anchors.fill: parent
                    hoverEnabled: true
                    cursorShape: Qt.PointingHandCursor
                    drag.target: root.tabCardMovable ? __tabItem : null
                    drag.axis: __private.isHorizontal ? Drag.XAxis : Drag.YAxis
                    onEntered: __tabItem.hovered = true;
                    onExited: __tabItem.hovered = false;
                    onClicked: __tabView.currentIndex = index;
                    onPressed:
                        (mouse) => {
                            __tabView.currentIndex = index;
                            __tabItem.down = true;

                            if (root.tabCardMovable) {
                                const pos = __tabView.mapFromItem(__tabContainer, 0, 0);
                                __tabItem.parent = __tabView;
                                __tabItem.x = pos.x;
                                __tabItem.y = pos.y;
                            }
                        }
                    onPositionChanged: move();
                    onReleased: {
                        __keepMovingTimer.stop();
                        __tabItem.down = false;
                        __tabItem.parent = __tabContainer;
                        __tabItem.x = __tabItem.y = 0;
                        __tabView.forceLayout();
                    }

                    function move() {
                        if (pressed && root.tabCardMovable) {
                            if (!__keepMovingTimer.running)
                                __keepMovingTimer.restart();
                            const pos = __tabView.mapFromItem(__tabItem, width * 0.5, height * 0.5);
                            const item = __tabView.itemAt(pos.x + __tabView.contentX + 1, pos.y + __tabView.contentY + 1);
                            if (item) {
                                if (item.index !== __tabContainer.index) {
                                    __tabModel.move(item.index, __tabContainer.index, 1);
                                }
                            }
                        }
                    }

                    Timer {
                        id: __keepMovingTimer
                        repeat: true
                        interval: 100
                        onTriggered: __dragHandler.move();
                    }
                }

                MosIconText {
                    id: __icon
                    leftPadding: __icon.status === Loader.Null ? 0 : root.defaultTabLeftPadding
                    anchors.left: parent.left
                    anchors.verticalCenter: parent.verticalCenter
                    color: __tabItem.colorText
                    iconSize: __tabContainer.tabIconSize
                    iconSource: __tabContainer.tabIcon
                    verticalAlignment: Text.AlignVCenter

                    Behavior on color { enabled: root.animationEnabled; ColorAnimation { duration: MosTheme.Primary.durationFast } }
                }

                MosText {
                    id: __text
                    width: root.tabSize === MosTabView.Size_Auto ? implicitWidth :
                                                                      Math.max(0, __tabContainer.tabFixedWidth
                                                                               - __tabItem.calcIconWidth - __tabItem.calcCloseWidth)
                    leftPadding: __icon.status === Loader.Null ? root.defaultTabLeftPadding : 0
                    rightPadding: __close.visible ? 0 : root.defaultTabRightPadding
                    anchors.left: __icon.right
                    anchors.leftMargin: __icon.status === Loader.Null ? 0 : __tabContainer.tabIconSpacing
                    anchors.verticalCenter: parent.verticalCenter
                    text: tabTitle
                    font {
                        family: root.themeSource.fontFamily
                        pixelSize: parseInt(root.themeSource.fontSize)
                    }
                    horizontalAlignment: {
                        switch (root.tabAlign) {
                        case MosTabView.Align_Left: return Text.AlignLeft;
                        case MosTabView.Align_Right: return Text.AlignRight;
                        default: return Text.AlignHCenter;
                        }
                    }
                    color: __tabItem.colorText
                    elide: Text.ElideRight

                    Behavior on color { enabled: root.animationEnabled; ColorAnimation { duration: MosTheme.Primary.durationFast } }
                }

                MosCaptionButton {
                    id: __close
                    visible: __tabContainer.tabEditable
                    enabled: visible
                    implicitWidth: visible ? (implicitContentWidth + leftPadding + rightPadding) : 0
                    topPadding: 0
                    bottomPadding: 0
                    leftPadding: 2
                    rightPadding: 2
                    anchors.right: parent.right
                    anchors.rightMargin: root.defaultTabRightPadding
                    anchors.verticalCenter: parent.verticalCenter
                    animationEnabled: root.animationEnabled
                    hoverCursorShape: Qt.PointingHandCursor
                    iconSize: tabIconSize
                    iconSource: MosIcon.CloseOutlined
                    colorIcon: hovered ? root.themeSource.colorTabCloseHover : root.themeSource.colorTabClose
                    onClicked: {
                        root.closeTabCallback(__tabContainer.index, __tabContainer.model);
                    }

                    HoverHandler {
                        cursorShape: Qt.PointingHandCursor
                    }
                }
            }
        }
    }

    Connections {
        target: root
        function onTabTypeChanged() {
            __private.needResetTabMaxWidth = true;
        }
        function onTabSizeChanged() {
            __private.needResetTabMaxWidth = true;
        }
    }

    QtObject {
        id: __private
        property bool isHorizontal: root.tabPosition == MosTabView.Position_Top ||
                                    root.tabPosition == MosTabView.Position_Bottom
        property int tabMaxWidth: 0
        property bool needResetTabMaxWidth: false
    }
}
