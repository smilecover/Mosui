import QtQuick
import QtQuick.Controls
import QtQuick.Layouts

import MosuiBasic

import '../../Controls'

Flickable {
    contentHeight: column.height
    ScrollBar.vertical: MosScrollBar { }

    Column {
        id: column
        width: parent.width - 15
        spacing: 30

        DocDescription {
            desc: qsTr(`
# MosCheckBox 多选框 \n
收集用户的多项选择。\n
* **模块 { MosuiBasic.Basic }**\n
* **继承自 { CheckBox }**\n
\n<br/>
\n### 支持的代理：\n
- 无\n
\n<br/>
\n### 支持的属性：\n
属性名 | 类型 | 默认值 | 描述
------ | --- | :---: | ---
animationEnabled | bool | MosTheme.animationEnabled | 是否开启动画
effectEnabled | bool | true | 是否开启点击效果
hoverCursorShape | int | Qt.PointingHandCursor | 悬浮时鼠标形状(来自 Qt.*Cursor)
readOnly | bool | false | 是否只读(等同于!checkable)
indicatorSize | int | 18 | 指示器大小
elide | enum | Text.ElideNone | 设置文本的elide属性(参考Text文档)
colorText | color | - | 文本颜色
colorIndicator | color | - | 指示器颜色
colorIndicatorBorder | color | - | 指示器边框颜色
radiusIndicator | [MosRadius](internal://MosRadius) | - | 指示器圆角
contentDescription | string | '' | 内容描述(提高可用性)
                       `)
        }

        Description {
            title: qsTr('何时使用')
            desc: qsTr(`
- 在一组可选项中进行多项选择时。\n
- 单独使用可以表示两种状态之间的切换，和 [MosSwitch](internal://MosSwitch) 类似。\n
  区别在于切换 [MosSwitch](internal://MosSwitch) 会直接触发状态改变，而 MosCheckBox 一般用于状态标记，需要和提交操作配合。\n
                       `)
        }

        ThemeToken {
            source: 'MosCheckBox'
            historySource: 'https://github.com/mengps/MosuiBasic/blob/master/src/imports/MosCheckBox.qml'
        }

        Description {
            title: qsTr('代码演示')
        }

        CodeBox {
            width: parent.width
            desc: qsTr(`
最简单的用法。\n
通过 \`enabled\` 设置是否启用。\n
                       `)
            code: `
                import QtQuick
                import MosuiBasic

                Column {
                    spacing: 15

                    MosRadioBlock {
                        id: sizeHintRadio
                        initCheckedIndex: 1
                        model: [
                            { label: 'Small', value: 'small' },
                            { label: 'Normal', value: 'normal' },
                            { label: 'Large', value: 'large' },
                        ]
                    }

                    Row {
                        spacing: 10

                        MosCheckBox {
                            text: qsTr('Checkbox')
                            sizeHint: sizeHintRadio.currentCheckedValue
                        }

                        MosCheckBox {
                            text: qsTr('Disabled')
                            enabled: false
                            sizeHint: sizeHintRadio.currentCheckedValue
                        }

                        MosCheckBox {
                            text: qsTr('Disabled')
                            checkState: Qt.PartiallyChecked
                            enabled: false
                            sizeHint: sizeHintRadio.currentCheckedValue
                        }

                        MosCheckBox {
                            text: qsTr('Disabled')
                            checkState: Qt.Checked
                            enabled: false
                            sizeHint: sizeHintRadio.currentCheckedValue
                        }
                    }
                }
            `
            exampleDelegate: Column {
                spacing: 15

                MosRadioBlock {
                    id: sizeHintRadio
                    initCheckedIndex: 1
                    model: [
                        { label: 'Small', value: 'small' },
                        { label: 'Normal', value: 'normal' },
                        { label: 'Large', value: 'large' },
                    ]
                }

                Row {
                    spacing: 10

                    MosCheckBox {
                        text: qsTr('Checkbox')
                        sizeHint: sizeHintRadio.currentCheckedValue
                    }

                    MosCheckBox {
                        text: qsTr('Disabled')
                        enabled: false
                        sizeHint: sizeHintRadio.currentCheckedValue
                    }

                    MosCheckBox {
                        text: qsTr('Disabled')
                        checkState: Qt.PartiallyChecked
                        enabled: false
                        sizeHint: sizeHintRadio.currentCheckedValue
                    }

                    MosCheckBox {
                        text: qsTr('Disabled')
                        checkState: Qt.Checked
                        enabled: false
                        sizeHint: sizeHintRadio.currentCheckedValue
                    }
                }
            }
        }

        CodeBox {
            width: parent.width
            desc: qsTr(`
使用 \`ButtonGroup(QtQuick原生组件)\` 来实现全选效果，具体可参考 \`CheckBox\` 文档。\n
                       `)
            code: `
                import QtQuick
                
                import MosuiBasic

                Column {
                    spacing: 10

                    ButtonGroup {
                        id: childGroup
                        exclusive: false
                        checkState: parentBox.checkState
                    }

                    MosCheckBox {
                        id: parentBox
                        text: qsTr('Parent')
                        checkState: childGroup.checkState
                    }

                    MosCheckBox {
                        checked: true
                        text: qsTr('Child 1')
                        leftPadding: indicator.width
                        ButtonGroup.group: childGroup
                    }

                    MosCheckBox {
                        text: qsTr('Child 2')
                        leftPadding: indicator.width
                        ButtonGroup.group: childGroup
                    }

                    MosCheckBox {
                        text: qsTr('More...')
                        leftPadding: indicator.width
                        ButtonGroup.group: childGroup

                        MosInput {
                            width: 110
                            anchors.verticalCenter: parent.verticalCenter
                            anchors.left: parent.right
                            anchors.leftMargin: 10
                            visible: parent.checked
                            placeholderText: qsTr('Please input')
                        }
                    }
                }
            `
            exampleDelegate: Column {
                spacing: 10

                ButtonGroup {
                    id: childGroup
                    exclusive: false
                    checkState: parentBox.checkState
                }

                MosCheckBox {
                    id: parentBox
                    text: qsTr('Parent')
                    checkState: childGroup.checkState
                }

                MosCheckBox {
                    checked: true
                    text: qsTr('Child 1')
                    leftPadding: indicator.width
                    ButtonGroup.group: childGroup
                }

                MosCheckBox {
                    text: qsTr('Child 2')
                    leftPadding: indicator.width
                    ButtonGroup.group: childGroup
                }

                MosCheckBox {
                    text: qsTr('More...')
                    leftPadding: indicator.width
                    ButtonGroup.group: childGroup

                    MosInput {
                        width: 110
                        anchors.verticalCenter: parent.verticalCenter
                        anchors.left: parent.right
                        anchors.leftMargin: 10
                        visible: parent.checked
                        placeholderText: qsTr('Please input')
                    }
                }
            }
        }
    }
}
